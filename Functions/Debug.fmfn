/**
 * =====================================================
 * Debug ( )
 *
 * PARAMETERS:
 *		none
 *
 * RETURNS:
 *		(string) A list of informative values
 * 		about the current FMP environment.
 *
 * DEPENDENCIES:
 * 		Developer ()
 *
 * NOTES:
 *
 * RELEASE:
 *		2014-05-08 - Injecting LayoutID despite it not being an internal function.
 *		2014-03-04 - Updated recursion and added more functions.
 * 		2013-01-25 - Updated to be recursive and use better output. Included more functions
 * 		101009 - Initial by Matt Petrowsky
 * =====================================================
 *
 */

If ( Developer;
	Let ( [
		~input = List (
			"Environment";
			"	ApplicationVersion";
			"	SystemPlatform";
			"	SystemVersion";
			"	ApplicationLanguage";
			"	SystemLanguage";
			"	HostName";
			"	HostApplicationVersion";
			"	SystemIPAddress";
			"	HostIPAddress";
			"	CurrentTimeStamp";
			"	CurrentHostTimeStamp";
			"	InstalledFMPlugins";
			"	AllowAbortState";
			"	ConnectionState";
			"	CustomMenuSetName";
			"	FileName";
			"	FileSize";
			"	FoundCount";
			"	SystemNICAddress";
			"	PersistentID";
			"	RecordOpenCount";
			"Errors";
			"	LastError";
			"	LastODBCError";
			"User";
			"	UserCount";
			"	AccountName";
			"	AccountPrivilegeSetName";
			"	CurrentPrivilegeSetName";
			"	AccountExtendedPrivileges";
			"	CurrentExtendedPrivileges";
			"Layout";
			"	LayoutName";
			"	LayoutNumber";
			"	LayoutTableName";
			"	LayoutViewState";
			"	LayoutAccess";
			"	LayoutTableName";
			"	ActiveLayoutObjectName";
			"	ActivePortalRowNumber";
			"Windows";
			"	WindowName";
			"	WindowMode";
			"	WindowOrientation";
			"	WindowStyle";
			"	WindowZoomLevel";
			"	WindowVisible";
			"	WindowTop";
			"	WindowLeft";
			"	WindowHeight";
			"	WindowWidth";
			"	WindowContentHeight";
			"	WindowContentWidth";
			"	WindowDesktopHeight";
			"	WindowDesktopWidth";
			"Records";
			"	RecordID";
			"	RecordNumber";
			"	RecordAccess";
			"	RecordModificationCount";
			"	RecordOpenState";
			"	SortState";
			"	TotalRecordCount";
			"Field";
			"	ActiveFieldName";
			"	ActiveFieldTableName";
			"	ActiveFieldContents";
			"	ActiveRepetitionNumber";
			"	ActiveSelectionStart";
			"	ActiveSelectionSize";
			"	CalculationRepetitionNumber";
			"Script";
			"	ScriptName";
			"	ScriptParameter";
			"	ScriptResult";
			"Paths";
			"	TemporaryPath";
			"	DesktopPath";
			"	DocumentsPath";
			"	FileMakerPath";
			"	FilePath";
			"	PreferencesPath";
			"	SystemDrive";
		);
		~padding = "                              "; // 30 chars is longest function name
		~tab = "	";
		~line = GetValue ( ~input ; $~debugCounter );
		~eval = If ( Left ( ~line ; 1 ) = ~tab ; // evaluate lines prefixed with tabs
			Let ( [
				~function = Trim ( ~line );
				~result = Evaluate ( "Get(" & ~function & ")" )
			];
				// Output
				~function
				& Left ( ~padding ; Length ( ~padding ) - Length ( ~function ) ) // spacing out result
				& If ( ValueCount ( ~result ) > 1; // some functions return multiple lines
					Substitute ( ~result ; ¶ ; ¶ & ~tab & Left ( ~padding ; Length ( ~padding ) - 1 ) );
					~result
				)
			);

			~line
		);
		~exit = $~debugCounter = ValueCount ( ~input )
	];
		Let ( [
			$~debugCounter = If ( ~exit ; "" ; $~debugCounter + 1 );
			$~debugOutput = List ( $~debugOutput ; ~eval )
		];
				If ( ~exit ;
					Substitute (
						$~debugOutput & Let ( $~debugOutput = "" ; "");
					[ "Layout¶" ; "Layout¶"
									& ~tab
									& "LayoutID"
									& Left ( ~padding ; Length ( ~padding ) - Length ( "LayoutID" ) - 1 )
									&  GetValue ( LayoutIDs ( Get ( FileName ) ) ; Get ( LayoutNumber ) )
									&¶ ]
					) ;
				Debug )
		)
	)
)
